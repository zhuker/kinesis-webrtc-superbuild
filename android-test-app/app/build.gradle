plugins {
    id 'com.android.application'
}

android {
    namespace 'com.kvs.webrtctest'
    compileSdk 34
    ndkVersion "28.2.13676358"

    defaultConfig {
        applicationId "com.kvs.webrtctest"
        minSdk 26
        targetSdk 34
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a"
        }

        externalNativeBuild {
            cmake {
                def args = ["-DBUILD_SAMPLE=OFF",
                            "-DBUILD_STATIC_LIBS=ON",
                            "-DBUILD_TEST=ON",
                            "-DBUILD_ANDROID_JNI_TEST=ON",
                            "-DENABLE_SIGNALING=OFF"]
                if (project.hasProperty('enableAsan') && project.property('enableAsan') == 'true') {
                    args += "-DADDRESS_SANITIZER=ON"
                }
                arguments(*args)
            }
        }
    }

    externalNativeBuild {
        cmake {
            path file("../../CMakeLists.txt")
            version "3.22.1"
        }
    }

    buildTypes {
        debug {
            debuggable true
        }
        release {
            minifyEnabled false
        }
    }

    sourceSets {
        main {
            assets.srcDirs += ["${project.rootDir}/../amazon-kinesis-video-streams-webrtc-sdk-c/samples"]
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packaging {
        jniLibs {
            useLegacyPackaging true
        }
    }
}

dependencies {
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
}
